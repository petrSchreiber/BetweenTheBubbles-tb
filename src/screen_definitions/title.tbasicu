#include once "../ui/textitem.tbasicu"

type ScreenTitle

  contextPtr as UInt32
  proposedScreenChange        as string
  proposedScreenChangeReason  as string

  titleFont   as UInt32
  menuFont    as UInt32
  
  titleText   as TextItem
  playText    as TextItem
  quitText    as TextItem
  langText    as TextItem
 
  function prepare(contextPtr as UInt32, optional screenChangeReason as string)
    me.contextPtr = contextPtr
    dim metadata as GameContext at me.contextPtr
    
    tbgl_backColor 128, 128, 255

    me.titleFont = tbgl_fontHandle("Arial", metadata.screenHeight/22)
    tbgl_buildFont(me.titleFont, 1)
    
    me.titleText.put("Between the Bubbles", 1)
    
    me.menuFont = tbgl_fontHandle("Arial", metadata.screenHeight/30)
    tbgl_buildFont(me.menuFont, 2)
    
    me.playText.put(i18nh("[P]lay"), 2)
    me.quitText.put(i18nh("[Q]uit"), 2)
    me.langText.put(i18nh("[L]anguage"), 2)
    
  end function

  function cleanup()
    tbgl_backColor 0, 0, 0

    tbgl_killFont(2)
    tbgl_killFont(1)
  end function

'

  function handleInput()
    dim metadata as GameContext at me.contextPtr

    if tbgl_getWindowKeyOnce(metadata.screenWindow, %VK_Q) then metadata.gameRunning = FALSE
    
    if tbgl_getWindowKeyOnce(metadata.screenWindow, %VK_P) then me.proposedScreenChange = "game"
    if tbgl_getWindowKeyOnce(metadata.screenWindow, %VK_L) then me.proposedScreenChange = "language"

  end function

  function render()
    dim metadata as GameContext at me.contextPtr

    tbgl_clearFrame
    
      me.titleText.render(metadata.screenWidth/2 - me.titleText.width/2, metadata.screenHeight*0.75 - me.titleText.height/2 + sin(metadata.gameTime) * metadata.screenHeight/40)
      
      me.playText.render(metadata.screenWidth/2 - me.playText.width/2, metadata.screenHeight*0.5 - me.playText.height/2)
      me.langText.render(metadata.screenWidth/2 - me.playText.width/2, metadata.screenHeight*0.35 - me.playText.height/2)
      me.quitText.render(metadata.screenWidth/2 - me.quitText.width/2, metadata.screenHeight*0.25 - me.quitText.height/2)
      
    tbgl_drawFrame
  end function

  function changeTo() as string
    return me.proposedScreenChange
  end function
  
  function changeReason() as string
    return me.proposedScreenChangeReason
  end function

end type

dim uScreenTitle as ScreenTitle

function screen_title_prepare(metadata as UInt32, screenChangeReason as string)
  reset uScreenTitle
  uScreenTitle.prepare(metadata, screenChangeReason)
end function

function screen_title_cleanup()
  uScreenTitle.cleanup()
end function

'

function screen_title_handleInput()
  uScreenTitle.handleInput()
end function

function screen_title_render()
  uScreenTitle.render()
end function

function screen_title_changeTo() as string
  return uScreenTitle.changeTo()
end function

function screen_title_changeReason() as string
  return uScreenTitle.changeReason()
end function

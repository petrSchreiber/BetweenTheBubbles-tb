uses "tbgl"

Randomize timer

#Include "entityComponentSystem/entityComponentSystem.tbasicu"

#include "entityComponentSystem/entityTypes/entity_player.tbasicu"
#include "entityComponentSystem/entityTypes/entity_leaf.tbasicu"
#include "entityComponentSystem/entityTypes/entity_forceArea_circular.tbasicu"

function tbMain()
  
  ' -- Create and show window
  dWord hWnd = tbgl_createWindowEx("ECS experiment - press ESC to quit", 1280, 720, 32, %TBGL_WS_WINDOWED | %TBGL_WS_CLOSEBOX)
  tbgl_showWindow

  #region "create and initialize ECS"
  dim ecs as tEntityComponentSystem()

  ' PLAYER
  dim p as tEntity_Player
  p.initialize()
  ecs.AddEntityPtr(varptr(p))
    
  ' LEAVES
  dim leaves(30) as tEntity_Leaf
  int32 i

  for i = 1 to UBound(leaves)
    leaves(i).initialize()
    
    leaves(i).position.x = rndf(-7, 7)
    leaves(i).position.y = rndf(-7, 7)
  
    ecs.AddEntityPtr(varptr(leaves(i)))
  next
  
  ' FORCE EFFECTORS
  dim forceEffectorA as tEntity_ForceArea_Circular
  forceEffectorA.Initialize(3, 10, 1, 0)
  forceEffectorA.position.x = -7
  forceEffectorA.position.y = -7

  ecs.AddEntityPtr(varptr(forceEffectorA))

  dim forceEffectorB as tEntity_ForceArea_Circular
  forceEffectorB.Initialize(3, 10, 1, 0)
  forceEffectorB.position.x = -5
  forceEffectorB.position.y =  5

  ecs.AddEntityPtr(varptr(forceEffectorB))

  dim forceEffectorC as tEntity_ForceArea_Circular
  forceEffectorC.Initialize(3, 10, 1, 0)
  forceEffectorC.position.x =  5
  forceEffectorC.position.y =  5

  ecs.AddEntityPtr(varptr(forceEffectorC))

  dim forceEffectorD as tEntity_ForceArea_Circular
  forceEffectorD.Initialize(3, 10, 1, 0)
  forceEffectorD.position.x =  5
  forceEffectorD.position.y = -5

  ecs.AddEntityPtr(varptr(forceEffectorD))
  #endregion
  
  tbgl_useVSync(True)
  tbgl_backcolor 64, 128, 255

  int32 frameCounter
  double frameRate
  
  ' -- Main loop
  while tbgl_isWindow(hWnd) 
    frameRate = tbgl_getFrameRate

    forceEffectorA.rotation =  -30 + sin(timer+1) * 10
    forceEffectorB.rotation = -120 + sin(timer+2) * 10
    forceEffectorC.rotation =  120 + sin(timer+3) * 10
    forceEffectorD.rotation =   30 + sin(timer+4) * 10
    
    incr frameCounter
    if frameCounter > 60 then
      tbgl_setWindowTitle(hWnd, format$(FrameRate, "0") + " FPS")
      frameCounter = 0
    end if

    tbgl_clearFrame 
      tbgl_camera(0, -25, 25, 0, 0, 0)

      ecs.ProcessEntities(frameRate)
    tbgl_drawFrame

    ' -- ESCAPE key to exit application
    if tbgl_getWindowKeyState(hWnd, %VK_ESCAPE) then exit while
    if tbgl_getWindowKeyState(hWnd, %VK_UP)     then p.position.y += 3/frameRate
    if tbgl_getWindowKeyState(hWnd, %VK_DOWN)   then p.position.y -= 3/frameRate
    if tbgl_getWindowKeyState(hWnd, %VK_LEFT)   then p.position.x -= 3/frameRate
    if tbgl_getWindowKeyState(hWnd, %VK_RIGHT)  then p.position.x += 3/frameRate

  wend

  tbgl_destroyWindow
end function

